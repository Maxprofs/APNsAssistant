script "button Feedback Behavior"
on mouseUp

   #<check if target folder exists>
      put fld "Folder" into tFolder
      if there is not a folder tFolder then
         answer "Please select the target folder with your certificate files" with "OK"
         set the menuHistory of btn "Menu" to 1
         focus on fld "Folder"
         exit mouseUp
      end if #there is not a folder tFolder
   #</check if target folder exists>


   #<save settings>
      put the folder into tSavedFolder
      set the folder to tFolder
   #</save settings>


   put the long date && the long time & LF & "Devices for removal unless re-registered" & LF into fld "FeedbackData" # visual feedback that following data is current

   
   #<query the server>
      get shell("openssl s_client -connect feedback.sandbox.push.apple.com:2196 -cert AppCertKey.pem")
      put it into tData
   #</query the server>
   

   put lineOffset(LF & "    Verify return code: 0 (ok)" & LF & "---" & LF, tData) into tOffset # strip off openssl info


   if tOffset > 0 then
      delete line 1 to tOffset + 2 of tData # remove the extra openssl info
      
      
      repeat until length(tData) < 38 # extract data for each device

         get binaryDecode("N", byte 1 to 4 of tData, tTime) # timestamp when the APNs determined that the application no longer exists on the device
         
         get binaryDecode("n", byte 5 to 6 of tData, tLength) # length of device token

         #<device token>
            put 7 + tLength - 1 into tLastByte
            get binaryDecode("H*", byte 7 to tLastByte of tData, tToken)
            delete byte 1 to tLastByte of tData
         #</device token>

         put tTime & comma & tToken & LF after fld "FeedbackData" # display the device data
      end repeat #until length(tData) < 38 
   end if #tOffset > 0 
   
   put "---END---" after fld "FeedbackData"
   
   set the folder to tSavedFolder
end mouseUp